#include <Servo.h>

#define PIN_LED 9
#define PIN_TRIG 12
#define PIN_ECHO 13
#define PIN_SERVO 10

#define SND_VEL 346.0
#define INTERVAL 25
#define PULSE_DURATION 10

#define _DIST_MIN 180.0
#define _DIST_MAX 360.0

#define TIMEOUT ((INTERVAL/2) * 1000.0)
#define SCALE (0.001 * 0.5 * SND_VEL)

#define _EMA_ALPHA 0.30

#define _DUTY_MIN 300
#define _DUTY_NEU 1500
#define _DUTY_MAX 2600

#define _ATTACH_MIN_US 500
#define _ATTACH_MAX_US 2500

Servo myservo;

float dist_prev = _DIST_MAX;
float dist_ema  = _DIST_MAX;
unsigned long last_sampling_time = 0;

static inline int angleToUS(float deg) {
  if (deg < 0) deg = 0;
  if (deg > 180) deg = 180;
  float us = _DUTY_MIN + (deg / 180.0f) * (_DUTY_MAX - _DUTY_MIN);
  return (int)(us + 0.5f);
}

float USS_measure(int TRIG, int ECHO) {
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(PULSE_DURATION);
  digitalWrite(TRIG, LOW);
  unsigned long dur = pulseIn(ECHO, HIGH, (unsigned long)TIMEOUT);
  if (dur == 0) return 0.0;
  return dur * SCALE;
}

void setup() {
  pinMode(PIN_LED, OUTPUT);
  pinMode(PIN_TRIG, OUTPUT);
  pinMode(PIN_ECHO, INPUT);
  digitalWrite(PIN_TRIG, LOW);
  digitalWrite(PIN_LED, HIGH);
  myservo.attach(PIN_SERVO, _ATTACH_MIN_US, _ATTACH_MAX_US);
  myservo.writeMicroseconds(_DUTY_NEU);
  Serial.begin(57600);
  last_sampling_time = millis();
}

void loop() {
  if (millis() < last_sampling_time + INTERVAL) return;

  float dist_raw = USS_measure(PIN_TRIG, PIN_ECHO);

  float dist_filtered = dist_prev;
  bool in_range = false;
  if (dist_raw >= _DIST_MIN && dist_raw <= _DIST_MAX) {
    dist_filtered = dist_raw;
    dist_prev = dist_raw;
    in_range = true;
  }

  dist_ema = _EMA_ALPHA * dist_filtered + (1.0f - _EMA_ALPHA) * dist_ema;

  float angle_deg;
  if (dist_ema <= _DIST_MIN) {
    angle_deg = 0.0f;
  } else if (dist_ema >= _DIST_MAX) {
    angle_deg = 180.0f;
  } else {
    angle_deg = (dist_ema - _DIST_MIN) * 180.0f / (_DIST_MAX - _DIST_MIN);
  }

  int servo_us = angleToUS(angle_deg);
  myservo.writeMicroseconds(servo_us);

  digitalWrite(PIN_LED, in_range ? LOW : HIGH);

  float cap_raw = dist_raw > (_DIST_MAX + 100) ? (_DIST_MAX + 100) : dist_raw;
  float cap_ema = dist_ema > (_DIST_MAX + 100) ? (_DIST_MAX + 100) : dist_ema;
  Serial.print("Min:");   Serial.print(_DIST_MIN);
  Serial.print(",dist:"); Serial.print(min(dist_raw, _DIST_MAX + 100));
  Serial.print(",ema:");  Serial.print(min(dist_ema, _DIST_MAX + 100));
  Serial.print(",Servo:");Serial.print(myservo.read());
  Serial.print(",Max:");  Serial.print(_DIST_MAX);
  Serial.println("");

  last_sampling_time += INTERVAL;
}
